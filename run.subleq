 !  #  "([SUBLEQ BOOT COMPILER].bcal  .D".+  .| 1$$4 $7&$v!$d  @% C %F% I %L  O% R$ U %X!%[#$('$v  ||%g  j% m |p"ds"nv%%(
0____5____10___5____20___5____30___5____40___5____50___5____60___5____70___5____80___5____90___5____100__5____110__5____
................................ !"#$%&'()*+,-..0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`abcdefghijklmnopqrstuvwx

================================================================================
The first line contains the boot loop to interpret the rest of this file; the
next two lines are there to help identify which character is at which address,
and which is the ASCII character corresponding to that address. Note that we
have replaced non-printable characters with '.', as well as the slash
(ASCII character 47), which is specially interpreted by the code.

The algorithm being executed is the following:
```pseudo
  A, B <- FIRST_POS
  while True:
    B <- B + 1
    U <- mem[B]
    if U == 47:        # (U is the slash character)
      mem[A] <- V
      A <- A + 1
      V <- 0
    elif 48 <= U < 58: # (U is a digit between 0 and 9)
      V <- 10 * V + (U - 48)
    elif U >= 126:     # (U is the tilde character)
      jump to FIRST_POS
    else:
      V <- 0
```

Indirect addressing is complex in subleq, as it requires to modify
instructions so that their source or target address is the one we want
to load. Fortunately, we can side-step this complexity here by directly
storing B inside the instruction to read it, and likewise for A. Since
storing a value in memory requires first reading the existing value, we
use two variables, A and A', each stored inside their respective instruction.

In our case, we end up storing B in location 46 (ASCII character '.'), while
A and A' and stored respectively in location 100 (ASCII character 'd') and
110 (ASCII character 'n').

We store the rest of the variables and useful constants at the beginning of
the program, starting from location 32 (ASCII character ' ', the first printable
ASCII character).

 Location | ASCII |   Contents
----------+-------+--------------
       46 |  '.'  | Variable B
      100 |  'd'  | Variable A
      110 |  'n'  | Variable A'
       32 |  ' '  | Variable Z (temporary variable)
       33 |  '!'  | Constant 1
       34 |  '"'  | Constant -1
       35 |  '#'  | Constant 10
       36 |  '$'  | Variable U
       37 |  '%'  | Variable V
       38 |  '&'  | Constant 46
       39 |  '''  | Constant 68

```
 !  #  "(
```
The code begins by initializing the constants 1, -1, and 10 by substracting the
initial value of the temporary variable Z from their initial value. After having
initialized -1, which is <= 0, the execution jumps to location 40 (ASCII
character '('), just after the place where constants are stored.

```
".+  .| 1$$4 $7
40___5____50___
()*+,-..0123456
```
This is the beginning of the loop. This first increases B by 1, then executes
`U <- mem[B]` in the following manner:
```
Z -= Z
Z -= mem[B]
U -= U
U -= Z
```

Note how, for each of the instructions where we don't want to branch, we put the
address of the instruction immediately after it as the branch target. We can
also see here that B is initialized to the ASCII character '|', which is
location 124.

```
&$v!$d
```

The next two instructions substract 46, then 1 from U, and branch to 'v' if
U <= 46, or 'd' if U = 47. If neither instruction branched, U has now been
reduced by 47, and the ASCII characters '0'..'9' now correspond to
1 <= U <= 10.

As this point, we execute `V <- 10 * V + U - 1`:
```
  @% C %F% I %L  O% R$ U %X!%[
61__5____70___5____80___5____90
=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ
```
This is straightforward non-branching code, which executes the following:
```
Z -= Z
Z -= V # (now Z = -v, where v is the initial value of V)
V -= Z # (now V = 2v)
Z -= V # (now Z = -3v)
V -= Z # (now V = 5v)
Z -= Z
Z -= V # (now Z = -5v)
Z -= U # (now Z = -5v - U)
V -= Z # (now V = 10v + U)
V -= 1
```

```
#$('$v  |
91__5____
[\]^_`abc
```
We then substract 10, then 68 from U, and loop back to the beginning if U <= 10
(meaning that the character was indeed in the range '0'..'9'), or branch to 'v'
if U <= 78 (meaning that the character was any character before the tilde
character). If neither branch happened, we branch to location 124 and start
executing the code we loaded.

```
|%g  j% m |p"ds"nv
100__5____110__5__
defghijklmnopqrstu
```

At this point, we execute `mem[A] <- V` followed by `A <- A + 1`, before
falling through the next case with `V <- 0`. This is done as follows:
```
V -= mem[A]
Z -= Z
Z -= V
mem[A'] -= Z
A <- A + 1
A' <- A' + 1
```

```
%%(
118
vwx
```

Finally, we arrive at address `v`, where we reset V to 0 and jump back to the
beginning of the loop.

================================================================================

We can now start writing a better bootstrap assembler. We would like the
following features:
- Ability to specify negative numbers. While it is not critical to have them,
  we may need negative numbers in data positions or to perform IO.
- Ability to specify PC-relative addresses, especially for fallthrough.
- Simple one-character labels.
- Line comments, so that we need less to be careful about not using slash.

The syntax of the new bootstrap assembler is the following:
- Line comments start with ';'.
- One-character labels declared by :[char]. Only characters in the range
  from '<' to '}' are allowed as label names, so this makes 66 possible labels.
  A label references the position of the next compiled value.
- The slash character, which compiles the V variable as previously in the next
  compilation position.
- Any character up to ',' will simply reset V to 0.
- The '-' character will perform V <- -V.
- The '.' character will add the position where the next instruction will be
  compiled to V.
- Any digit 0-9 will do V <- 10*V + [digit].
- Any character which is a possibly valid label will add the position
  of that label to V.
- The tilde character will start execution.





We use the beginning of the memory as registers. We have several useful
global variables, HERE which holds the first free address to write code in,
and POS, the current scanning position.

 Location |   Contents
----------+--------------
        0 | Constant 0
        1 | Constant 1
        2 | Constant -1
       16 | Variable HERE
       17 | Variable POS
       18 | Variable LAST

Load constants:
124| 0/Z 0/Z 127/next
127| 2/const-1 2/const-1 131/next 1/
130| 130/ 2/const-1 134/next
134| 1/const1 1/const1 137/next
137| 2/const-1 1/const1 140/next

Initialise HERE with variable A (first free position for compilation)
140| 16/HERE 16/HERE 143/next
143| 100/oldA 0/Z 146/next
146| 0/ 16/HERE 149/next
149| 0/Z 0/Z 152/next

Initialise POS with variable B (position in the file)
152| 17/POS 17/POS 155/next
155| 46/oldB 0/Z 158/next
158| 0/ 17/POS 161/next
161| 0/Z 0/Z 164/next
164| 2/const-1 17/POS 167/next

167| 0/Z 0/Z 209/

Subroutine jumps to subroutine at location S are done by storing -R in location S-1,
where R is the return address.

; Location 171 (nextchar): read a character at POS and store it in LAST
170| 0/retaddr # Return address stored here
; Copy POS -> loc 186
171| 186/readloc 186/readloc 174/next
174| 17/POS 0/Z 177/next
177| 0/Z 186/readloc 180/next
180| 0/Z 0/Z 183/next
; Copy *186 -> LAST
183| 18/LAST 18/LAST 186/next
186| 0/readloc 0/Z 189/next
189| 0/Z 18/LAST 192/next
192| 0/Z 0/Z 195/next
; POS += 1
195| 2/const-1 17/POS 198/next
; Return
198| 206/ret 206/ret 201/next
201| 170/retaddr 206/ret 204/next
204| 170/retaddr 170/retaddr 0/ret

; Save copies of HERE and POS 
207| 0/HERE_BAK 0/POS_BAK
209| 16/HERE 207/HERE_BAK 212/next
212| 17/POS 208/POS_BAK 215/next
215| 0/Z 0/Z 300/

; Storage for label array
218| ///// /////  ///// /////  ///// /////  ///// /////
248| ///// /////  ///// /////  ///// /////

; Definitions of constants (with space for a few more)
288| 46/10/66/
291| 59/217/9/
294| 44/283//
297| ///

; Label computation loop
300| 303/retaddr 170/171/nextchar 304/next
304| 288/const46 18/LAST 300/loop
307| 1/const1 18/LAST 325/increment
310| 289/const10 18/LAST 300/loop
313| 1/const1 18/LAST 331/newlabel
316| 1/const1 18/LAST 377/comment
319| 290/const66 18/LAST 300/loop
322| 0/Z 0/Z 390/exit
; increment: LAST was a slash: increment HERE
325| 2/const-1 16/HERE 328/next
328| 0/Z 0/Z 300/loop
; newlabel
331| 334/retaddr 170/171/nextchar 335/next
335| 291/const59 18/LAST 300/loop
338| 18/LAST 0/Z 341/next
341| 292/const217 0/Z 344/next
; zero position to write then write HERE inside
344| 368/labelpos 368/labelpos 347/next
347| 369/labelpos 369/labelpos 350/next
350| 372/labelpos 372/labelpos 353/next
353| 0/Z 368/labelpos 356/next
356| 0/Z 369/labelpos 359/next
359| 0/Z 372/labelpos 362/next
362| 0/Z 0/Z 365/next
365| 16/HERE 0/Z 368/next
368| 0/labelpos 0/labelpos 371/next
371| 0/Z 0/labelpos 374/next
374| 0/Z 0/Z 300/loop
; comment
377| 380/retaddr 170/171/nextchar 381/next
381| 293/const9 18/LAST 377/comment
384| 1/const1 18/LAST 300/loop
387| 0/Z 0/Z 377/comment

; exit, all labels have been computed
; restore HERE and POS
390| 16/HERE 16/HERE 393/next
393| 17/POS 17/POS 396/next
396| 207/HERE_BAK 16/HERE 399/next
399| 208/POS_BAK 17/POS 402/next

; Compilation loop

; clear
402| 405/V 405/V 406/next 0/
; loop
406| 409/retaddr 170/171/nextchar 410/next
410| 294/const44 18/LAST 402/clear
413| 1/const1 18/LAST 437/negate
416| 1/const1 18/LAST 456/addhere
419| 1/const1 18/LAST 465/increment
422| 289/const10 18/LAST 504/digit
425| 1/const1 18/LAST 534/newlabel
428| 1/const1 18/LAST 541/comment
431| 290/const66 18/LAST 554/addlabel
434| 0/Z 0/Z 578/exit

; negate: V <- -V
437| 405/V 0/Z 440/next
440| 443/TMP 443/TMP 444/next 0/
444| 0/Z 443/TMP 447/next
447| 405/V 405/V 450/next
450| 443/TMP 405/V 453/next
453| 0/Z 0/Z 406/loop

; addhere: V <- V + POS
456| 16/HERE 0/Z 459/next
459| 0/Z 405/V 462/next
462| 0/Z 0/Z 406/loop

; increment
; zero HERE then write V
465| 492/herepos 492/herepos 468/next
486| 493/herepos 493/herepos 471/next
471| 496/herepos 496/herepos 474/next
474| 16/HERE 0/Z 477/next
477| 0/Z 492/herepos 480/next
480| 0/Z 493/herepos 483/next
483| 0/Z 496/herepos 486/next
486| 0/Z 0/Z 489/next
489| 405/V 0/Z 492/next
492| 0/herepos 0/herepos 495/next
495| 0/Z 0/herepos 498/next
; increment HERE
498| 2/const-1 16/HERE 501/next
501| 0/Z 0/Z 402/clear

; digit
; at this point, we have -9 <= LAST <= 0,
; we need to compute V <- 10*V + LAST + 9
504| 405/V 0/Z 507/next ; now Z = -v
507| 0/Z 405/V 510/next ; now V = 2v
510| 405/V 0/Z 513/next ; now Z = -3v
513| 0/Z 405/V 516/next ; now V = 5v
516| 0/Z 0/Z 519/next
519| 405/V 0/Z 522/next
522| 18/LAST 0/Z 525/next
525| 293/const9 0/Z 528/next
528| 0/Z 405/V 531/next
531| 0/Z 0/Z 406/loop

; newlabel
534| 537/retaddr 170/171/nextchar 538/next
538| 0/Z 0/Z 402/clear

; comment
541| 544/retaddr 170/171/nextchar 545/next
545| 293/const9 18/LAST 541/comment
548| 1/const1 18/LAST 402/clear
551| 0/Z 0/Z 541/comment

; addlabel
; at this point, LAST = pos in label array - 65
; so we need to compute 218 + LAST + 65 = 283 + LAST
554| 18/LAST 0/Z 557/next
557| 295/const283 0/Z 560/next
560| 569/labelpos 569/labelpos 563/next
563| 0/Z 569/labelpos 566/next
566| 0/Z 0/Z 569/next
569| 0/labelpos 0/Z 572/next
572| 0/Z 405/V 575/next
575| 0/Z 0/Z 406/loop

; jump
578| 207/HERE_BAK 583/jump 581/next
581| 0/Z 0/Z 0/jump

~

; At this point we have a functional, if minimalistic, assembler.
; Let's use it to write a small Forth kernel.

; We keep the previous array of variables, and add some new ones.
;  Location |   Contents
; ----------+--------------
;         0 | Constant 0
;         1 | Constant 1
;         2 | Constant -1
;         3 | Variable TMP
;         4 | Variable R1
;         5 | Variable R2
;        10 | Variable LR   Kept at zero except when calling B 
;        16 | Variable HERE
;        17 | Variable POS
;        18 | Variable LAST
;        19 | Variable SP
;        20 | Variable RP
;        21 | Variable A
;        22 | Variable B
;        23 | Variable C
;        24 | Variable I
;        25 | Variable DICT
;        26 | Variable STATE

; This is the entry point; jump to the start of the code.
0/Z 0/Z E/

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We start by allocating the normal (S) and return (R) stacks,
; 256 words each.
; Variables SP and RP should point to the first free location (S and R at the beginning).
:S
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
:R
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////
//////// //////// //////// ////////


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Calling convention
; A function X is called by jumping to location X, and storing the
; opposite of the return address at location X-1, which can be assumed
; to hold 0, as shown below:
; ```
; 3./ 1-X/ X/ [return address]/
; ```
;
; Inside the function, returning is done by computing the opposite
; of the return address, and jumping to it, making sure to reset it
; to zero:
; ```
; 8./ret 7./ret 1./next
; 1-X/ 4./ret 1./next
; 1-X/ 1-X/ 0/ret
; ```

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; @ -> Do R1 := -mem[-R1]
0/retaddr :@

9./loadptr 8./loadptr 1./next
4/R1 5./loadptr 1./next
4/R1 4/R1 1./next
0/loadptr 4/R1 1./next

; Return
8./ret 7./ret 1./next
1-@/retaddr 4./ret 1./next
1-@/retaddr 1-@/retaddr 0/ret


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; > -> Do mem[-R2] <- -R1
0/retaddr :>

; TMP <- -R1
3/TMP 3/TMP 1./next
4/R1 3/TMP 1./next

; Indirect store *(-R2) <- TMP
12./storeptr1 11./storeptr1 1./next
16./storeptr2 15./storeptr2 1./next
5/R2 5./storeptr1 1./next
5/R2 9./storeptr2 1./next
0/storeptr1 3/TMP 1./next
3/TMP 0/Z 1./next
0/Z 0/storeptr2 1./next
0/Z 0/Z 1./next

; Return
8./ret 7./ret 1./next
1->/retaddr 4./ret 1./next
1->/retaddr 1->/retaddr 0/ret


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; B -> beginning of word
; effect: rpush(I); I = (-LR) + 1; goto mem[-LR]
; Sequence at beginning of word:
; 3./ 10/LR B/begin 1./


:B

5/R2 5/R2 1./next
20/RP 5/R2 1./next
4/R1 4/R1 1./next
24/I 4/R1 1./next
3./ 1->/ >/ 1./ ; indirect_store mem[-R2] := -R1

; Increment RP
2/const-1 20/RP 1./next

; I = (-LR)
24/I 24/I 1./next
10/LR 24/I 1./next
; Clear LR
10/LR 10/LR N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; N -> next
; effect: goto mem[I++]

:N

4/R1 4/R1 1./next
24/I 4/R1 1./next
3./ 1-@/ @/ 1./next ; R1 := -mem[I]
2/const-1 24/I 1./next ; I++
4/R1 4./jmpaddr 1./next
2./jmpaddr 1./jmpaddr 0/jmpaddr ; goto -R1

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Representation of a dictionary word
;  Length | Contents
; --------+----------
;     LEN | name
;       1 | LEN
;       1 | immediate flag
;       1 | LINK (previous word) or 0
;         | body         <--- Pointer to dict at beginning of body

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; EXIT word (e)
69/E 88/X 73/I 84/T 4/LEN 0/not_immediate 0/LINK :e

; Clear I
24/I 24/I 1./next
; Decrement RP
1/const1 20/RP 1./next

; Clear R1
4/R1 4/R1 1./next
20/RP 4/R1 1./next
3./ 1-@/ @/ 1./ ; indirect_load R1 := -mem[-R1]
4/R1 24/I 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; >A word (X): pop variable A from normal stack
62/> 65/A 2/LEN 0/not_immediate e/LINK :X

; Clear A
21/A 21/A 1./next
; Decrement SP
1/const1 19/SP 1./next

; Clear R1
4/R1 4/R1 1./next
19/SP 4/R1 1./next
3./ 1-@/ @/ 1./ ; indirect_load R1 := -mem[-R1]
4/R1 21/A 1./next
 
0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; R>A word (Y): pop variable A from return stack
82/R 62/> 65/A 3/LEN 0/not_immediate X/LINK :Y

; Clear A
21/A 21/A 1./next
; Decrement RP
1/const1 20/RP 1./next

; Clear R1
4/R1 4/R1 1./next
20/RP 4/R1 1./next
3./ 1-@/ @/ 1./ ; indirect_load R1 := -mem[-R1]
4/R1 21/A 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; A> word (x): push variable A to normal stack
65/A 62/> 2/LEN 0/not_immediate Y/LINK :x

5/R2 5/R2 1./next
19/SP 5/R2 1./next
4/R1 4/R1 1./next
21/A 4/R1 1./next
3./ 1->/ >/ 1./ ; indirect_store mem[-R2] := -R1

; Increment SP
2/const-1 19/SP 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; A>R word (y): push variable A to return stack
65/A 62/> 82/R 3/LEN 0/not_immediate x/LINK :y

5/R2 5/R2 1./next
20/RP 5/R2 1./next
4/R1 4/R1 1./next
21/A 4/R1 1./next
3./ 1->/ >/ 1./ ; indirect_store mem[-R2] := -R1

; Increment RP
2/const-1 20/RP 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; LDA word (l): A := mem[I++]
76/L 68/D 65/A 3/LEN 0/not_immediate y/LINK :l

; Clear A
21/A 21/A 1./next

; A := mem[I]
4/R1 4/R1 1./next
24/I 4/R1 1./next
3./ 1-@/ @/ 1./ ; indirect_load R1 := -mem[-R1]
4/R1 21/A 1./next

; Increment I
2/const-1 24/I 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; GT0B word (b): target := mem[I++]; if A > 0: I := target
71/G 84/T 48/0 66/B 4/LEN 0/not_immediate l/LINK :b

; R1 := -mem[I]
4/R1 4/R1 1./next
24/I 4/R1 1./next
3./ 1-@/ @/ 1./ ; indirect_load R1 := -mem[-R1]
; Increment I
2/const-1 24/I 1./next

0/Z 21/A N/next ; exit if A <= 0

24/I 24/I 1./next
4/R1 24/I 1./next
0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; @A (a): A := mem[A]
64/@ 65/A 2/LEN 0/not_immediate b/LINK :a

; R1 := -mem[A]
4/R1 4/R1 1./next
21/A 4/R1 1./next
3./ 1-@/ @/ 1./ ; indirect_load R1 := -mem[-R1]
21/A 21/A 1./next
4/R1 21/A 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; !A word (z): mem[B] <- A
33/! 65/A 2/LEN 0/not_immediate a/LINK :z

5/R2 5/R2 1./next
22/B 5/R2 1./next
4/R1 4/R1 1./next
21/A 4/R1 1./next
3./ 1->/ >/ 1./ ; indirect_store mem[-R2] := -R1

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; -A word (m): A -= B
45/- 65/A 2/LEN 0/not_immediate z/LINK :m

22/B 21/A 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; $ word (r): A, B, C := C, A, B
36/$ 1/LEN 0/not_immediate m/link :r

; R1 <- -A
4/R1 4/R1 1./next
21/A 4/R1 1./next
; R2 <- -B
5/R2 5/R2 1./next
22/B 5/R2 1./next
; B <- -R1
22/B 22/B 1./next
4/R1 22/B 1./next
; R1 <- -C
4/R1 4/R1 1./next
23/C 4/R1 1./next
; A <- -R1
21/A 21/A 1./next
4/R1 21/A 1./next
; C <- -R2
23/C 23/C 1./next
5/R2 23/C 1./next

0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; EQ0 word (Z): A := (A == 0)
69/E 81/Q 48/0 3/LEN 0/not_immediate m/LINK :Z

0/Z 21/A 4./jmp
21/A 21/A N/next
; Now we know A <= 0
2/const-1 21/A 4./jmp
; Here A was equal to 0, now is equal to 1
0/Z 0/Z N/next
; A was < 0
21/A 21/A N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; DECA word (d): A -= 1
; This word could be easily emulated using -A, but doing so would
; clobber B; instead, it is incredibly useful for this word to be
; available for memory comparison.
68/D 69/E 67/C 65/A 4/LEN 0/not_immediate Z/LINK :d

1/const1 21/A 1./jmp
0/Z 0/Z N/next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MEM<-CMP (C)
; Inputs: A, B, C, assume A >= 0
; Output: A <- mem[B-A:B[ == mem[C-A:C[

77/M 69/E 77/M 60/< 45/- 67/C 77/M 80/P 8/LEN 0/not_immediate d/LINK :C

3./ 10/LR B/begin 1./

; Start: if A = 0, then result is 1
b/GT0B 4./ ; if A > 0, jump over next 3 instrs
  l/LDA 1/ e/EXIT
; We know A > 0, compare first char
; rotate A,B,C, decrement, save pointer on stack and load first char
r/$ d/DECA x/A> a/@ 
; again
r/$ d/DECA x/A> a/@
m/-A Z/EQ0 b/GT0B 6./ ; if chars were equal, jump over next 5 instrs
  ; Chars were different: clean stack, set A to 0 and exit
  X/>A X/>A l/LDA 0/ e/EXIT
; Restore B, C
X/>A r/$ r/$ X/>A r/$ r/$
; Decrement A, branch if chars remain to compare
d/DECA b/GT0B 4C/start
; Exit with 1
l/LDA 1/ e/EXIT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; w -> compile 
; effect: mem[HERE++] := -R1
; TODO make word instead

0/retaddr :w

5/R2 5/R2 1./next
16/HERE 5/R2 1./next
3./ 1->/ >/ 1./ ; indirect_store mem[-R2] := -R1

; Increment HERE
2/const-1 16/HERE 1./next

; Return
8./ret 7./ret 1./next
1-w/retaddr 4./ret 1./next
1-w/retaddr 1-w/retaddr 0/ret





;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; n -> new word
; Inputs: R1
0/retaddr :n



:E
; Printing a character:
; 3./ 1-/ 2./ [XX].
3./ 1-/ 2./ 83/S
3./ 1-/ 2./ 116/t
3./ 1-/ 2./ 97/a
3./ 1-/ 2./ 103/g
3./ 1-/ 2./ 101/e
3./ 1-/ 2./ 32/ 
3./ 1-/ 2./ 49/1
3./ 1-/ 2./ 46/.
3./ 1-/ 2./ 46/.
3./ 1-/ 2./ 46/.
3./ 1-/ 2./ 10/

; Initialize SP and RP
19/SP 19/SP 2./next S-/const-S
1-./ 19/SP 1./next
20/RP 20/RP 2./next R-/const-R
1-./ 20/RP 1./next 
; Initialize LR
10/LR 10/LR 1./next


; Testing
3./ 10/LR B/begin 1./
x/A> l/LDA 17/ x/A> a/@ x/A>

l/LDA 32/ r/$ l/LDA 32/ r/$ l/LDA 4/
C/MEM<-CMP x/A>

1./


; Main loop

; : READ-WORD-RAW start: GET-CHAR if(IS-SP, jmp start) PUSH1 loop: GET-CHAR if(IS-SP, EXIT, ADD1 jmp loop) POS SWAP ; 
; BOOT: loop (READ-WORD-RAW FIND-WORD-RAW EXECUTE)

; boot loop: read/find/execute only??
; TODO: numbers? -> with a word?
; TODO: read word
; TODO: find word
; TODO: if state && not immediate then execute else compile-word
; LOOP



0/Z 0/Z 1-/

~